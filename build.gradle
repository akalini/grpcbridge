buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.9.4'
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.google.protobuf'
    apply plugin: 'idea'

    version = '1.3.9'
    group = 'grpcbridge'

    repositories {
        mavenCentral()
    }

    ext {
        ver = [
                protobuf   : '3.24.0',
                grpc       : '1.58.0',
                opencensus : '0.26.0',
                slf4j      : '1.7.30',
        ]
    }

    dependencies {
        implementation "org.slf4j:slf4j-api:${ver.slf4j}"

        implementation "com.google.protobuf:protobuf-java:${ver.protobuf}"
        implementation "com.google.protobuf:protobuf-java-util:${ver.protobuf}"
        implementation "io.grpc:grpc-protobuf:${ver.grpc}"

        testImplementation 'junit:junit:4.12'
        testImplementation 'org.assertj:assertj-core:3.14.0'
    }

    protobuf {
        protoc {
            artifact = "com.google.protobuf:protoc:${ver.protobuf}"
        }

        plugins {
            grpc {
                artifact = "io.grpc:protoc-gen-grpc-java:${ver.grpc}"
            }
        }

        generateProtoTasks {
            all()*.plugins {
                grpc {}
            }
        }
    }

    idea {
        module {
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/test/java")
            sourceDirs += file("${protobuf.generatedFilesBaseDir}/test/grpc")
        }
    }
}
